plugins {
    id 'org.asciidoctor.jvm.convert'
}

repositories {
    mavenCentral()
}

configurations {
    documentation
}

dependencies {
    documentation "org.codehaus.groovy:groovy:$groovyVersion"
    documentation "org.codehaus.groovy:groovy-ant:$groovyVersion"
    documentation "org.codehaus.groovy:groovy-templates:$groovyVersion"
    documentation "com.github.javaparser:javaparser-core:$javaParserCoreVersion"
}

asciidoctor {
    baseDirFollowsSourceDir()
    resources {
        from('src/docs/images')
        into "./images"
    }

    attributes 'experimental'  : 'true',
               'compat-mode'   : 'true',
               'toc'           : 'left',
               'icons'         : 'font',
               'version'       : project.version,
               'sourcedir'     : "${rootProject.allprojects.find { it.name == 'grace-scaffolding-core'}.projectDir}/src/main/groovy"
}

task apidocs(type: Groovydoc) {
    def allProjects = rootProject.allprojects.findAll { project ->
      ['grace-scaffolding-core'].contains(project.name)
    }
    source allProjects.collect { project ->
      project.files('src/main/groovy')
    } 

    destinationDir = new File(buildDir, 'docs/api')
    // Might need a classpath
    docTitle = "Grace Scaffolding ${version}"

    access = GroovydocAccess.PROTECTED
    includeAuthor = true
    includeMainForScripts = true
    processScripts = true
    classpath = configurations.documentation
    groovyClasspath = configurations.documentation
}

task docs(type:Copy, dependsOn:[apidocs, asciidoctor])  {
    from "$buildDir/asciidoc/html5"
    into "$buildDir/docs"
}